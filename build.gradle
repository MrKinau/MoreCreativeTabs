plugins {
    id "io.github.pacifistmc.forgix" version "1.2.6"
    id "me.hypherionmc.modutils.modpublisher" version "1.0.3"
}

// Jenkins
ext {
    release = project.properties['release'] ?: false
}

def version_base = "${project.base_version}"
def final_ver = "${version_base}.${project.version_patch}"

if (System.getenv('BUILD_NUMBER') != null && project.release == false) {
    final_ver = version_base + "." + System.getenv('BUILD_NUMBER') + "d"
}

forgix {
    group = project.group
    mergedJarName = "${mod_name}-combo-${minecraft_version}-${final_ver}.jar"
    outputDir = "artifacts/merged"

    forge {
        projectName = "Forge"

        mixin "${mod_id}.mixin.json"
        mixin "${mod_id}.forge.mixin.json"
    }

    fabric {
        projectName = "Fabric"
    }
}

subprojects {
    apply plugin: 'java'
    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    version = final_ver

    jar {
        from(rootProject.file("LICENSE")) {
            rename { "${it}_${mod_name}" }
        }
        manifest {
            attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_author,
                'Specification-Version'   : project.jar.archiveVersion,
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_author,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Timestamp'               : System.currentTimeMillis(),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Built-On-Minecraft'      : minecraft_version
            ])
        }
    }

    repositories {
        mavenCentral()
        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release = 17
    }

    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    task preClean {
        delete "$rootDir/artifacts"
    }
}

publisher {
    apiKeys {
        curseforge = System.getenv("CURSE_TOKEN")
        modrinth = System.getenv("MODRINTH_TOKEN")
    }

    curseID = curse_id
    modrinthID = modrinth_id
    versionType = rel_type
    changelog = rootProject.file(changelog_file)
    version = "${minecraft_version}-${final_ver}"
    displayName = "[1.18.1 - 1.18.2] MoreCreativeTabs - ${final_ver}"
    gameVersions = ["1.18.1", "1.18.2"]
    loaders = ["forge", "fabric", "quilt"]
    artifact = forgix.getOutputDir() + "/" + forgix.getMergedJarName()
}

publishMod.dependsOn(mergeJars)
