subprojects {
    apply plugin: 'java'
    java.toolchain.languageVersion = JavaLanguageVersion.of(17)

    // Jenkins
    ext {
        release = project.properties['release'] ?: false
    }

    def version_base = "${project.version}"
    version = "${version_base}.${project.version_patch}"

    if (System.getenv('BUILD_NUMBER') != null && project.release == false) {
        version = version_base + "." + System.getenv('BUILD_NUMBER') + "d"
    }

    jar {
        from(rootProject.file("LICENSE")) {
            rename { "${it}_${mod_name}" }
        }
        manifest {
            attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_author,
                'Specification-Version'   : project.jar.archiveVersion,
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_author,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Timestamp'               : System.currentTimeMillis(),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Built-On-Minecraft'      : minecraft_version
            ])
        }
    }

    repositories {
        mavenCentral()
        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release = 17
    }

    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    task preClean {
        delete "$buildDir/libs"
        delete "$rootDir/artifacts"
    }

    task copyAllArtifacts(type: Copy) {
        if (project.name !== "Common") {
            from "$buildDir/libs"
            into "$rootDir/artifacts"
            include("*.jar")
            delete("$rootDir/artifacts/*-common-*.jar")
        }
    }

    build.finalizedBy(copyAllArtifacts)
}
