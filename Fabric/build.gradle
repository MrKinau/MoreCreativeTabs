plugins {
    id 'fabric-loom' version '1.0-SNAPSHOT'
    id 'maven-publish'
    id 'idea'
    id "com.modrinth.minotaur" version "2.+"
    id "me.hypherionmc.cursegradle" version "2.0.1"
}

archivesBaseName = "${mod_name}-fabric-${minecraft_version}"

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":Common")
}

loom {
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }

    mixin {
        defaultRefmapName = "morecreativetabs.refmap.json"
    }
}

processResources {
    from project(":Common").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }

    filesMatching('*.mixins.json') {
        expand "refmap_target": "${archivesBaseName}-"
    }
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

jar {
    from("LICENSE") {
        rename { "${it}_${mod_name}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }

    task delDevJar {
        doLast {
            def tree = fileTree("build/libs")
            tree.include '**/*-dev.jar'
            tree.each { it.delete() }
        }
    }

    build.finalizedBy delDevJar
}

modrinth {
    debugMode = false
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "nOiHJ1dx"
    versionNumber = "${minecraft_version}-${version}f"
    versionType = "release"
    changelog = rootProject.file("changelog-fabric.md").text.trim()
    uploadFile = remapJar
    versionName = "[Fabric/Quilt ${minecraft_version}.x] MoreCreativeTabs - ${project.version}"
    gameVersions = ["1.19", "1.19.1", "1.19.2"]
    loaders = [ "fabric", "quilt" ]
    dependencies {
        required.project "P7dR8mSH"
    }
}

curseforge {
    if (System.getenv("CURSE_TOKEN") !== null) {
        apiKey = System.getenv("CURSE_TOKEN")
    } else {
        apiKey = ""
    }
    project {
        id = "581838"
        releaseType = "release"
        addGameVersion "1.19"
        addGameVersion "1.19.1"
        addGameVersion "1.19.2"
        addGameVersion "Fabric"
        addGameVersion "Quilt"
        addGameVersion "Java 17"
        changelogType = "markdown"
        changelog = rootProject.file("changelog-fabric.md")
        mainArtifact(remapJar) {
            displayName = "[Fabric/Quilt ${minecraft_version}.x] MoreCreativeTabs - ${project.version}"
        }
    }
    options {
        forgeGradleIntegration = false
        debug = false
        fabricIntegration = false
    }
}

afterEvaluate {
    tasks.curseforge581838.dependsOn remapJar
}
